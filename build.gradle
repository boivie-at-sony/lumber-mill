

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group="com.sonymobile"
    version="$lumberMillVersion"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile 'com.jayway.awaitility:awaitility:1.7.0'
        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:1.7.1'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'org.codehaus.groovy:groovy-test:2.4.6'
        testCompile 'ch.qos.logback:logback-classic:1.1.7'
        testCompile 'ch.qos.logback:logback-core:1.1.7'
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'Lumber Mill'
                    packaging 'jar'
                    description 'Where Logs are cut into Lumber'
                    url 'https://github.com/sonyxperiadev/lumber-mill'

                    scm {
                        connection 'scm:git:git@github.com:sonyxperiadev/lumber-mill.git'
                        developerConnection 'scm:git:git@github.com:sonyxperiadev/lumber-mill.git'
                        url 'git@github.com:sonyxperiadev/lumber-mill.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'jrask'
                            name 'Johan Rask'
                            email 'johan.rask@sonymobile.com'
                            organization 'Sony Mobile Communications, Inc'
                            organizationUrl 'https://github.com/sonyxperiadev'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
